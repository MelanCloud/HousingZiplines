// Zipline v1.1.2
// Novys (mncd)

/*
Setup:
    var zip/x = destination x
    var zip/y = destination y
    var zip/z = destination z
    var zip/active = 1

Optional Setup:
    var zip/slack = slack factor (default: 5) [range: -inf to inf]
        -x  : works intuitively (goes upwards)
        1   : no slack
        5   : normal slack
        10  : heavy slack
    var zip/mag = magnitude factor (10) [-inf to inf]
        <-5 : may cause issues
        -x  : works intuitively (inverse slope acceleration)
        1   : base acceleration
        10  : x10 slope acceleration
        100 : x100 slope acceleration
    var zip/decay = decay factor (10) [-inf to inf]
        -x  : works intuitively (reverses decay)
        1   : no decay
        10  : normal decay
        50  : extremely light decay
        >50 : may cause issues
    var zip/speed = set initial speed (1000) [0+]
        -x  : does not support negative values
*/

// recur 5t
// goto function "Zipline"

if (var zip/active == 0 0, var zip/dist = 0 0) {
    exit
}

if () {
// velocities & distance
    globalvar zip/px = var zip/x 0
    globalvar zip/px -= %player.location.x%
    var zip/vx = globalvar zip/px 0
    var zip/py = var zip/y 0
    var zip/py -= %player.location.y%
    var zip/vy = var zip/py 0
    var zip/slope = var zip/py 0
    var zip/pz = var zip/z 0
    var zip/pz -= %player.location.z%
    var zip/vz = var zip/pz 0
    var zip/pz *= var zip/pz 0
    globalvar zip/px *= globalvar zip/px 0
    var zip/py *= var zip/py 0
    globalvar zip/px += var zip/pz 0
    globalvar zip/px += var zip/py 0

// SQRT
    teamvar a temp = 3037000498
    teamvar a temp /= globalvar zip/px 0
    teamvar a temp += 1

    globalvar zip/px *= %var.team/a temp 0%
    globalvar zip/px *= %var.team/a temp 0%

    teamvar b temp = globalvar zip/px 0
    teamvar b temp /= 537752656
    teamvar b temp += 880298

    globalvar a = globalvar zip/px 0
    globalvar a /= %var.team/b temp 0%
}
if () {
    globalvar a += %var.team/b temp 0%

    teamvar b temp = globalvar zip/px 0
    teamvar b temp /= globalvar a 0
    globalvar a /= 4
    globalvar a += %var.team/b temp 0%

    globalvar b = globalvar zip/px 0
    globalvar b /= globalvar a 0
    globalvar b += globalvar a 0

    teamvar b temp = globalvar zip/px 0
    teamvar b temp /= globalvar b 0
    globalvar b /= 4
    globalvar b += %var.team/b temp 0%

    var zip/dist = globalvar zip/px 0
    var zip/dist /= globalvar b 0
    var zip/dist += globalvar b 0

    globalvar zip/px /= var zip/dist 0
    var zip/dist /= 4
    var zip/dist += globalvar zip/px 0

    var zip/dist += 1
    var zip/dist /= %var.team/a temp 0%
// SQRT END

// failsafes
    teamvar b temp = var zip/dist 0
    teamvar b temp -= var zip/dist/s 0
    var zip/dist/s /= var zip/dist/s 0
    teamvar b temp *= var zip/dist/s 0
}
if () {
    globalvar a = %var.team/b temp 0%
    globalvar a /= globalvar a 0
    globalvar a *= -1
    globalvar a += 1
    var zip/stuck *= globalvar a 0
    var zip/stuck += globalvar a 0

    var zip/slope *= var zip/mag 10
    var zip/slope *= -1
}

// vector projection
if (var zip/active == 1 0) {
    var zip/x0 = %player.location.x%
    var zip/y0 = %player.location.y%
    var zip/z0 = %player.location.z%

    // direction vector
    var zip/dx = var zip/x 0
    var zip/dx -= var zip/x0 0
    var zip/dy = var zip/y 0
    var zip/dy -= var zip/y0 0
    var zip/dz = var zip/z 0
    var zip/dz -= var zip/z0 0
    var zip/spd = var zip/speed 1000
}
if () {
    // player vector
    globalvar zip/px = %player.location.x%
    globalvar zip/px -= var zip/x0 0
    var zip/py = %player.location.y%
    var zip/py -= var zip/y0 0
    var zip/pz = %player.location.z%
    var zip/pz -= var zip/z0 0

    // (p * d) (globalvar a as dot)
    globalvar a = globalvar zip/px 0
    globalvar a *= var zip/dx 0

    globalvar b = var zip/py 0
    globalvar b *= var zip/dy 0
    globalvar a += globalvar b 0

    globalvar b = var zip/pz 0
    globalvar b *= var zip/dz 0
    globalvar a += globalvar b 0

    // |d|^2 (teamvar c temp as len2)
    teamvar c temp = var zip/dx 0
    teamvar c temp *= var zip/dx 0
    teamvar a temp = var zip/dy 0
    teamvar a temp *= var zip/dy 0
    teamvar c temp += %var.team/a temp 0%
    teamvar a temp = var zip/dz 0
    teamvar a temp *= var zip/dz 0
    teamvar c temp += %var.team/a temp 0%

    // dot / |d|^2 (var zip/speed as progress)
    var zip/speed = globalvar a 0
    var zip/speed *= 1000
    var zip/speed /= %var.team/c temp 0%
}

if (var zip/active == 1) {
// acceleration
    var zip/dist/o = var zip/dist 0
    var zip/slope /= var zip/dist/o 0

// fallback
    var zip/active = 200
    globalvar a = var zip/dist 0
    globalvar a -= 17
    globalvar a /= 3
    globalvar a *= 5
    globalvar b *= 5

    teamvar c temp = var zip/slope 0
    teamvar c temp *= 10
    teamvar c temp /= var zip/mag 10
    teamvar c temp *= -20
    
    var zip/active += globalvar a 0
    var zip/active += globalvar b 0
    var zip/active += %var.team/c temp 0%

    var zip/mag Unset
} else {
    var zip/slope /= var zip/dist/o 0
    var zip/dist/s = var zip/dist 0

// failsafes
    var zip/dist/f = var zip/spd 0
    var zip/dist/f /= 5000
    var zip/dist/f += 1

    var zip/bo = var zip/spd 0
    var zip/bo /= -1000
    var zip/bo -= 1
}

// failsafes
if or (var zip/active <= 0 0, teamvar b temp < var zip/bo -3, var zip/stuck >= 8 0) {
    sound "Horse Saddle" 0.7 1 "invokers_location"
    var zip/dist = 0
}

// final
if (var zip/dist <= var zip/dist/f 1) {
    tp "custom_coordinates" "%var.player/zip/x% ~ ~"
    tp "custom_coordinates" "~ %var.player/zip/y% ~"
    tp "custom_coordinates" "~ ~ %var.player/zip/z%"
    tp "custom_coordinates" "~0.5 ~ ~0.5"
    changeVelocity 0 0 0

    // footprint cleanup
    var zip/dist Unset
    var zip/active Unset

    var zip/stuck Unset
    var zip/speed Unset
    var zip/dist/f Unset
    var zip/x Unset
    var zip/y Unset
    var zip/z Unset
    globalvar zip/px Unset
    var zip/py Unset
    var zip/pz Unset
    teamvar a temp Unset
    teamvar b temp Unset
    teamvar c temp Unset
    globalvar a Unset
    globalvar b Unset
} else {
    var zip/active -= 5
}

if (!var zip/active == 0 0) {
// velocity calculation
    var zip/spd /= var zip/dist 0
    var zip/vx *= var zip/spd 0
    var zip/vy *= var zip/spd 0
    var zip/vz *= var zip/spd 0
    var zip/spd *= var zip/dist 0

    globalvar a = var zip/vx 0
    globalvar a += 4611686018427387904
    globalvar a /= 4611686018427387904
    globalvar a *= 2
    globalvar a -= 1
    globalvar a *= 500
    var zip/vx += globalvar a 0
    var zip/vx /= 1000

    globalvar a = var zip/vy 0
    globalvar a += 4611686018427387904
    globalvar a /= 4611686018427387904
    globalvar a *= 2
} else {
    // footprint cleanup
    var zip/vx Unset
    var zip/vy Unset
    var zip/vz Unset

    var zip/slope Unset
    var zip/decay Unset
    var zip/bo Unset

    var zip/spd Unset
    var zip/slack Unset
    var zip/dist/s Unset
    var zip/dist/o Unset
}
if () {
    globalvar a -= 1
    globalvar a *= 500
    var zip/vy += globalvar a 0
    var zip/vy /= 1000

    globalvar a = var zip/vz 0
    globalvar a += 4611686018427387904
    globalvar a /= 4611686018427387904
    globalvar a *= 2
    globalvar a -= 1
    globalvar a *= 500
    var zip/vz += globalvar a 0
    var zip/vz /= 1000

// slack
    var zip/speed += 100
    teamvar a temp = 1000
    teamvar a temp -= var zip/speed 0
    teamvar a temp *= var zip/speed 0
    teamvar a temp /= 10000

    teamvar a temp *= var zip/slack 5
    teamvar a temp /= 100

    var zip/vy -= %var.team/a temp 0%
} else {
    // footprint cleanup
    var zip/x0 Unset
    var zip/y0 Unset
    var zip/z0 Unset
    var zip/dx Unset
    var zip/dy Unset
    var zip/dz Unset

    exit
}

if () {
// velocity change
    changeVelocity "%var.player/zip/vx 0%" "%var.player/zip/vy 0%" "%var.player/zip/vz 0%"
    pause 1
    changeVelocity "%var.player/zip/vx 0%" "%var.player/zip/vy 0%" "%var.player/zip/vz 0%"
    pause 1
    changeVelocity "%var.player/zip/vx 0%" "%var.player/zip/vy 0%" "%var.player/zip/vz 0%"
    pause 1
    changeVelocity "%var.player/zip/vx 0%" "%var.player/zip/vy 0%" "%var.player/zip/vz 0%"
    pause 1
    changeVelocity "%var.player/zip/vx 0%" "%var.player/zip/vy 0%" "%var.player/zip/vz 0%"
}

// acceleration
if (var zip/slope >= 0 0) {
    var zip/spd += 500

    var zip/slope *= 100
    var zip/spd += var zip/slope 0
} else {
    globalvar a = 8000
    globalvar a -= var zip/spd 0
    globalvar a /= var zip/decay 10
    var zip/spd += globalvar a 0
}

if () {
    var zip/spd -= 28000
    globalvar b = var zip/spd 0
    globalvar b /= globalvar b 0
    globalvar b -= 1
    globalvar b *= var zip/spd 0
    var zip/spd -= globalvar b 0
    var zip/spd += 28000
}
