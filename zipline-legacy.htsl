// Novys (mncd)

/*
Setup:
    var zip/x = destination x
    var zip/y = destination y
    var zip/z = destination z
    var zip/active = 1

Optional Setup (Use factors of 5/10):
    var zip/slack = slack factor (default: 5)
        0   : no slack
        1   : extremely light slack
        5   : normal slack
        10  : heavy slack
*/

// recur 5t
// goto function "Zipline"

if (var zip/active == 0 0, var zip/temp == 0 0) {
    // footprint cleanup
    var zip/speed Unset
    var zip/slack Unset

    exit
}

if (!var zip/temp == 0 0, var zip/active <= var zip/temp 0) {
    var zip/x = var zip/dest/x
    var zip/y = var zip/dest/y
    var zip/z = var zip/dest/z
}

if (var zip/active == 1) {
    var zip/dest/x = var zip/x
    var zip/dest/y = var zip/y
    var zip/dest/z = var zip/z
    var zip/x += %player.location.x%
    var zip/x /= 2
    var zip/y += %player.location.y%
    var zip/y /= 2
    var zip/z += %player.location.z%
    var zip/z /= 2
    var zip/speed = 1000
}

if () {
    // velocities & distance
    globalvar zip/dx = var zip/x
    globalvar zip/dx -= %player.location.x%
    teamvar xVelo temp = globalvar zip/dx
    var zip/dy = var zip/y
    var zip/dy -= %player.location.y%
    teamvar yVelo temp = var zip/dy
    var zip/slope = var zip/dy
    var zip/dz = var zip/z
    var zip/dz -= %player.location.z%
    teamvar zVelo temp = var zip/dz
    var zip/dz *= var zip/dz
    globalvar zip/dx *= globalvar zip/dx
    var zip/dy *= var zip/dy
    globalvar zip/dx += var zip/dz
    globalvar zip/dx += var zip/dy

    // SQRT
    teamvar multi temp = 3037000498
    teamvar multi temp /= globalvar zip/dx
    teamvar multi temp += 1

    globalvar zip/dx *= %var.team/multi temp%
    globalvar zip/dx *= %var.team/multi temp%

    teamvar temp0 temp = globalvar zip/dx
    teamvar temp0 temp /= 537752656
    teamvar temp0 temp += 880298

    globalvar temp = globalvar zip/dx
    globalvar temp /= %var.team/temp0 temp%
}
if () {
    globalvar temp += %var.team/temp0 temp%

    teamvar temp0 temp = globalvar zip/dx
    teamvar temp0 temp /= globalvar temp
    globalvar temp /= 4
    globalvar temp += %var.team/temp0 temp%

    globalvar temp2 = globalvar zip/dx
    globalvar temp2 /= globalvar temp
    globalvar temp2 += globalvar temp

    teamvar temp0 temp = globalvar zip/dx
    teamvar temp0 temp /= globalvar temp2
    globalvar temp2 /= 4
    globalvar temp2 += %var.team/temp0 temp%

    var zip/dist = globalvar zip/dx
    var zip/dist /= globalvar temp2
    var zip/dist += globalvar temp2

    globalvar zip/dx /= var zip/dist
    var zip/dist /= 4
    var zip/dist += globalvar zip/dx

    var zip/dist += 1
    var zip/dist /= %var.team/multi temp%
    // SQRT END

    var zip/dist *= 2
}

// slack
if (var zip/active == 1) {
    var zip/temp = var zip/dist
    var zip/temp *= var zip/slack 5
    var zip/temp /= 100
    var zip/y -= var zip/temp
    var zip/temp /= 4
    globalvar temp = var zip/dist
}

// dismount timer
if (var zip/active == 1, var zip/dist >= 0 0, var zip/dist < 18) {
    var zip/active = 20
    globalvar temp -= 1
    globalvar temp /= 2
    globalvar temp *= 5
    var zip/active += globalvar temp
    var zip/temp *= 5
    var zip/active += var zip/temp
    var zip/temp = var zip/active
    var zip/temp /= 2
}
if (var zip/active == 1, var zip/dist >= 18) {
    var zip/active = 60
    globalvar temp -= 17
    globalvar temp /= 3
    globalvar temp *= 5
    var zip/active += globalvar temp
    var zip/temp *= 5
    var zip/active += var zip/temp
    var zip/temp = var zip/active
    var zip/temp /= 2
}

// final
if (var zip/active == 0 0) {
    tp "custom_coordinates" "%var.player/zip/dest/x% ~ ~"
    tp "custom_coordinates" "~ %var.player/zip/dest/y% ~"
    tp "custom_coordinates" "~ ~ %var.player/zip/dest/z%"
    tp "custom_coordinates" "~0.5 ~ ~0.5"
    changeVelocity 0 0 0

    // footprint cleanup
    var zip/temp Unset
    globalvar temp Unset
    var zip/dest/x Unset
    var zip/dest/y Unset
    var zip/dest/z Unset
    var zip/x Unset
    var zip/y Unset
    var zip/z Unset
    globalvar zip/dx Unset
    var zip/dy Unset
    var zip/dz Unset
    var zip/dist Unset
    teamvar xVelo temp Unset
    teamvar yVelo temp Unset
    teamvar zVelo temp Unset
    teamvar multi temp Unset
    teamvar temp0 temp Unset
    globalvar temp2 Unset
    exit
} else {
    var zip/active -= 5 true
}

// velocity calculation
if () {
    var zip/speed /= var zip/dist
    teamvar xVelo temp *= var zip/speed
    teamvar yVelo temp *= var zip/speed
    teamvar zVelo temp *= var zip/speed
    var zip/speed *= var zip/dist

    globalvar temp = %var.team/xVelo temp%
    globalvar temp += 4611686018427387904
    globalvar temp /= 4611686018427387904
    globalvar temp *= 2
    globalvar temp -= 1
    globalvar temp *= 500
    teamvar xVelo temp += globalvar temp
    teamvar xVelo temp /= 1000

    globalvar temp = %var.team/yVelo temp%
    globalvar temp += 4611686018427387904
    globalvar temp /= 4611686018427387904
    globalvar temp *= 2
}
if () {
    globalvar temp -= 1
    globalvar temp *= 500
    teamvar yVelo temp += globalvar temp
    teamvar yVelo temp /= 1000

    globalvar temp = %var.team/zVelo temp%
    globalvar temp += 4611686018427387904
    globalvar temp /= 4611686018427387904
    globalvar temp *= 2
    globalvar temp -= 1
    globalvar temp *= 500
    teamvar zVelo temp += globalvar temp
    teamvar zVelo temp /= 1000
}

if () {
    changeVelocity "%var.team/xVelo temp%" "%var.team/yVelo temp%" "%var.team/zVelo temp%"
    pause 1
    changeVelocity "%var.team/xVelo temp%" "%var.team/yVelo temp%" "%var.team/zVelo temp%"
    pause 1
    changeVelocity "%var.team/xVelo temp%" "%var.team/yVelo temp%" "%var.team/zVelo temp%"
    pause 1
    changeVelocity "%var.team/xVelo temp%" "%var.team/yVelo temp%" "%var.team/zVelo temp%"
    pause 1
    changeVelocity "%var.team/xVelo temp%" "%var.team/yVelo temp%" "%var.team/zVelo temp%"
}

// acceleration
if (var zip/speed < 12000) {
    var zip/speed += 1000
}
